/**
 * This file is to publish the library to Maven on Artifactory
 */

def libVersion = '1.58'
def libGroupId = 'mega.android.core'

project('core-ui') {
    artifactoryPublish.dependsOn('build')
    artifactoryPublish.dependsOn('releaseSourcesJar')
    publishing {
        publications {
            aar(MavenPublication) {
                groupId = libGroupId
                artifactId = "ui"
                version = libVersion
                // Tell maven to prepare the generated "*.aar" file for publishing
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
                artifact("$buildDir/libs/${project.getName()}-sources.jar") {
                    classifier = "sources"
                    extension = "jar"
                }
            }
        }
    }

    artifactoryPublish {
        publications(publishing.publications.aar)
    }
}

artifactory {
    clientConfig.setIncludeEnvVars(true)

    contextUrl = "${System.env.ARTIFACTORY_BASE_URL}/artifactory/mega-gradle"
    publish {
        repository {
            repoKey = "core-ui" // The Artifactory repository key to publish to
            username = "${System.env.ARTIFACTORY_USER}" // The publisher user name
            password = "${System.env.ARTIFACTORY_ACCESS_TOKEN}" // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = [
                    'property1': "property 1",
                    'property2': "property 2",
                    'property3': "property 3",
            ]
            publishPom = true // Publish generated POM files to Artifactory (true by default)
        }
    }
}
